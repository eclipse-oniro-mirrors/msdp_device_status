# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../../device_status.gni")

config("intention_stationary_server_public_config") {
  include_dirs = [
    "include",
    "${device_status_root_path}/interfaces/innerkits/include",
    "${device_status_root_path}/libs/interface",
    "${device_status_root_path}/services/boomerang/include",
    "${device_status_root_path}/services/native/include",
  ]
}

ohos_source_set("intention_stationary_server") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  branch_protector_ret = "pac_ret"

  include_dirs = [
    "include",
    "${device_status_root_path}/interfaces/innerkits/include",
    "${device_status_root_path}/libs/interface",
    "${device_status_root_path}/services/boomerang/include",
    "${device_status_root_path}/services/native/include",
  ]

  sources = [
    "${device_status_root_path}/services/native/src/devicestatus_dumper.cpp",
    "${device_status_root_path}/services/native/src/devicestatus_hisysevent.cpp",
    "${device_status_root_path}/services/native/src/devicestatus_manager.cpp",
    "${device_status_root_path}/services/native/src/devicestatus_msdp_client_impl.cpp",
    "${device_status_root_path}/services/native/src/devicestatus_napi_manager.cpp",
    "src/stationary_server.cpp",
    "src/sensor_manager.cpp",
  ]

  public_configs = [ ":intention_stationary_server_public_config" ]

  defines = device_status_default_defines

  deps = [
    "${device_status_root_path}/intention/prototype:intention_prototype",
    "${device_status_root_path}/intention/stationary/data:intention_stationary_data",
    "${device_status_root_path}/utils/common:devicestatus_util",
    "${device_status_root_path}/utils/ipc:devicestatus_ipc",
  ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "ace_engine:ui_session",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "eventhandler:libeventhandler",
    "graphic_2d:librender_service_client",
    "hilog:libhilog",
    "input:libmmi-client",
    "os_account:os_account_innerkits",
    "samgr:samgr_proxy",
    "window_manager:libdm",
  ]

  if (device_status_hisysevent_enable) {
    external_deps += [ "hisysevent:libhisysevent" ]
    defines += [ "MSDP_HIVIEWDFX_HISYSEVENT_ENABLE" ]
  }

  if (device_status_hitrace_enable) {
    external_deps += [ "hitrace:hitrace_meter" ]
    defines += [ "MSDP_HIVIEWDFX_HITRACE_ENABLE" ]
  }

  if (device_status_motion_enable) {
    defines += [ "MOTION_ENABLE" ]
    external_deps += [ "motion:motion_interface_native" ]
  }

  if (device_status_sensor_enable) {
    defines += [ "DEVICE_STATUS_SENSOR_ENABLE" ]
    external_deps += [ "sensor:sensor_interface_native" ]
  }

  if (device_status_boomerang_onestep) {
    defines += [ "BOOMERANG_ONESTEP" ]
    external_deps += [
      "accessibility:accessibility_common",
      "accessibility:accessibleability",
      "accessibility:accessibilityclient",
      "window_manager:libwm",
    ]
    sources += [
      "${device_status_root_path}/services/boomerang/src/accessibility_manager.cpp",
    ]
  }

  if (device_status_boomerang_support_hdr) {
    defines += [ "BOOMERANG_SUPPORT_HDR" ]
    external_deps += [ "image_framework:image_utils" ]
  }

  subsystem_name = "${device_status_subsystem_name}"
  part_name = "${device_status_part_name}"
}
