# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# http://www.apache.org/licenses/LICENSE-2.0
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/msdp/device_status/device_status.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

subsystem_name = "msdp"
part_name = "device_status"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name/devicestatus"

copy_taihe_idl("copy_taihe") {
  sources = [ "idl/ohos.multimodalAwareness.deviceStatus.taihe" ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.multimodalAwareness.deviceStatus.ani.cpp",
    "$taihe_generated_file_path/src/ohos.multimodalAwareness.deviceStatus.abi.c",
  ]
}

taihe_shared_library("multimodalawareness_devicestatus_ani") {
  taihe_generated_file_path = "$taihe_generated_file_path"

  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = get_target_outputs(":run_taihe")

  include_dirs = [
    "include",
    "${device_status_utils_path}/include",
    "${device_status_interfaces_path}/innerkits/interaction/include",
    "${device_status_frameworks_path}/native/interaction/include",
  ]

  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.multimodalAwareness.deviceStatus.impl.cpp",
    "src/ani_error_utils.cpp",
    "src/ani_devicestatus_observer.cpp"
  ]

  deps = [
    ":run_taihe",
    "${device_status_interfaces_path}/innerkits:devicestatus_client",
    "${device_status_utils_path}:devicestatus_util",
  ]

  external_deps = [
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_single",
    "hiappevent:hiappevent_innerapi"
  ]

  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
}

generate_static_abc("multimodalawareness_devicestatus_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.multimodalAwareness.deviceStatus.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/multimodalawareness_devicestatus_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("multimodalawareness_devicestatus_etc") {
  source = "$target_out_dir/multimodalawareness_devicestatus_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":multimodalawareness_devicestatus_abc" ]
}

group("multimodalawareness_devicestatus_pack") {
  deps = [
    ":multimodalawareness_devicestatus_etc",
    ":multimodalawareness_devicestatus_ani",
  ]
}
