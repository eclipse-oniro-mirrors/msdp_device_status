# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# http://www.apache.org/licenses/LICENSE-2.0
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//base/msdp/device_status/device_status.gni")
copy_taihe_idl("metadataBinding_taihe") {
  sources = [ "idl/ohos.multimodalAwareness.metadataBinding.taihe" ]
}
subsystem_name = "msdp"
part_name = "device_status"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name/metadataBinding"
ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":metadataBinding_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.multimodalAwareness.metadataBinding.ani.cpp",
    "$taihe_generated_file_path/src/ohos.multimodalAwareness.metadataBinding.abi.c",
  ]
}
taihe_shared_library("MetadataBinding") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  include_dirs = [
    "include",
    "${device_status_utils_path}/include",
    "${device_status_root_path}/utils/common/include",
    "${device_status_interfaces_path}/innerkits/interaction/include",
    "${device_status_interfaces_path}/innerkits/include/",
    "${device_status_frameworks_path}/native/interaction/include"
  ]
  external_deps = [
    "c_utils:utils",
    "graphic_2d:librender_service_client",
    "graphic_2d:librender_service_base",
    "hilog:libhilog",
    "image_framework:image_native",
    "image_framework:image_taihe",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ani_boomerang.cpp",
    "src/ani_boomerang_manager.cpp",
    "src/ohos.multimodalAwareness.metadataBinding.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${device_status_interfaces_path}/innerkits:devicestatus_client",
    "${device_status_utils_path}:devicestatus_util",
  ]
}

generate_static_abc("metadataBinding_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.multimodalAwareness.metadataBinding.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/metadataBinding_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("metadataBinding_etc") {
  source = "$target_out_dir/metadataBinding_abc.abc"           
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":metadataBinding_abc" ]
}

generate_static_abc("metadataBinding_taihe_test") {
  base_url = "${device_status_frameworks_path}/ets/metadataBinding"
  files = [ "$base_url/test/test_main.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/metadataBinding_taihe_test.abc"
}

group("metadataBinding") {
  deps = [
    ":metadataBinding_etc",
    ":MetadataBinding",
    #":metadataBinding_taihe_test",
  ]
}