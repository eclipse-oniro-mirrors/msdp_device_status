# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# http://www.apache.org/licenses/LICENSE-2.0
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/msdp/device_status/device_status.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

copy_taihe_idl("coordination_taihe") {
  sources = [ "idl/ohos.cooperate.taihe" ]
}

subsystem_name = "msdp"
part_name = "device_status"
taihe_generated_file_path =
    "$taihe_file_path/out/$subsystem_name/$part_name/coordination"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":coordination_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.cooperate.ani.cpp",
    "$taihe_generated_file_path/src/ohos.cooperate.abi.c",
  ]
}

taihe_shared_library("Coordination_so") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  include_dirs = [
    "include",
    "${device_status_utils_path}/include",
    "${device_status_interfaces_path}/innerkits/interaction/include",
    "${device_status_frameworks_path}/native/interaction/include",
  ]
  external_deps = [
    "c_utils:utils",
    "graphic_2d:librender_service_base",
    "graphic_2d:librender_service_client",
    "hilog:libhilog",
    "image_framework:image_native",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ets_cooperate_manager.cpp",
    "src/ohos.cooperate.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${device_status_interfaces_path}/innerkits:devicestatus_client",
    "${device_status_utils_path}:devicestatus_util",
  ]
}

generate_static_abc("coordination_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.cooperate.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/coordination_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("Coordination_etc") {
  source = "$target_out_dir/coordination_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":coordination_abc" ]
}

generate_static_abc("coordination_test") {
  base_url = "${device_status_root_path}/frameworks/ets/coordination"
  files = [ "$base_url/test/test_main.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/coordination_test.abc"
}

group("coordination_ani") {
  deps = [
    ":Coordination_etc",
    ":Coordination_so",
  ]
}
